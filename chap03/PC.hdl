// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    // 条件を逆順で通していく
    Inc16(in=prev, out=incremented);
    Mux16(a=prev, b=incremented, sel=inc, out=tempinc);
    Mux16(a=tempinc, b=in, sel=load, out=tempload);
    Mux16(a=tempload, b=false, sel=reset, out=tempreset);
    Register(in=tempreset, load=true, out=out, out=prev);
}
